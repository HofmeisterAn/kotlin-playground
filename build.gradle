buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${project.'kotlin.version'}"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:${project.'spotless.version'}"
  }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'kotlin'
  apply plugin: 'com.diffplug.gradle.spotless'

  version = '1.0'

  sourceCompatibility = 11
  targetCompatibility = 11

  jar {
    manifest {
      attributes('Implementation-Title': project.name, 'Implementation-Version': version)
    }
  }

  repositories {
    jcenter()
  }

  dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${project.'kotlin.version'}"

    testImplementation "org.junit.jupiter:junit-jupiter-params:${project.'junit5.version'}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${project.'junit5.version'}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${project.'junit5.version'}"
  }

  test {
    useJUnitPlatform()
    testLogging { showStandardStreams = true }
  }

  spotless {
    java {
      googleJavaFormat()
      indentWithSpaces(2)
      endWithNewline()
    }
    kotlin {
      ktlint().userData([
        'indent_size': '2',
        'continuation_indent_size': '2',
        'insert_final_newline': 'true',
        'max_line_length': 'off'
      ])
    }
    groovyGradle {
      target '*.gradle'
      greclipse()
      indentWithSpaces(2)
      endWithNewline()
      paddedCell()
    }
  }
}
